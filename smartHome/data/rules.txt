@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>.
@prefix ns: <http://www.semanticweb.org/camille/ontologies/2019/1/untitled-ontology-6#>.

[HeatingUpRule:
    (?room rdf:type ns:Room)
    (?room ns:contains ?userTemp)
    (?room ns:contains ?realTemp)
    (?room ns:contains ?heating)

	(?userTemp rdf:type ns:TemperatureRequest)
	(?userTemp ns:intensity ?userTempValue)
	(?realTemp rdf:type ns:Temperature)
	(?realTemp ns:intensity ?realTempValue)
	greaterThan(?userTempValue, ?realTempValue)

	(?heating rdf:type ns:Heating)
	(?heating ns:hasAction ?action)
	(?action rdf:type ns:HeatingUp)
	(?heating ns:hasAction ?down)
	(?down rdf:type ns:HeatingDown)
	
	->
	
	(?action rdf:type ns:RelevantAction)
	(?down rdf:type ns:Action)
]

[HeatingDownRule:
    (?room rdf:type ns:Room)
    (?room ns:contains ?userTemp)
    (?room ns:contains ?realTemp)
    (?room ns:contains ?heating)

	(?userTemp rdf:type ns:TemperatureRequest)
	(?userTemp ns:intensity ?userTempValue)
	(?realTemp rdf:type ns:Temperature)
	(?realTemp ns:intensity ?realTempValue)
	greaterThan(?realTempValue, ?userTempValue)

	(?heating rdf:type ns:Heating)
	(?heating ns:hasAction ?action)
	(?action rdf:type ns:HeatingDown)
	(?heating ns:hasAction ?up)
    (?up rdf:type ns:HeatingUp)

	->

	(?action rdf:type ns:RelevantAction)
    (?up rdf:type ns:Action)
]

[WakeupRule:
	(?wakeup rdf:type ns:Clock)
	(?wakeup ns:name "WakeupClock")
	(?wakeup ns:hour ?hourWu)
	(?wakeup ns:minute ?minuteWu)
	(?current rdf:type ns:Clock)
    (?current ns:name "CurrentClock")
	(?current ns:hour ?hourCur)
	(?current ns:minute ?minuteCur)
	equal(?hourWu, ?hourCur)
	equal(?minuteWu, ?minuteCur)

	(?shutter rdf:type ns:Shutter)
	(?shutter ns:hasAction ?action)
	(?action rdf:type ns:Open)

	(?light rdf:type ns:Light)
	(?room rdf:type ns:Bedroom)
	(?room ns:contains ?light)
	(?light ns:hasAction ?action2)
	(?action2 rdf:type ns:PowerOn)

	->

	(?action rdf:type ns:RelevantAction)
	(?action2 rdf:type ns:RelevantAction)
]